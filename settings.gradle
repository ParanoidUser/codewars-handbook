plugins {
    id "com.gradle.enterprise" version "3.0"
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"

        background {
            value "Git Commit", "git rev-parse --short=8 --verify HEAD".execute().text.trim()
            value "Git Branch", "git rev-parse --abbrev-ref HEAD".execute().text.trim()
        }

        obfuscation {
            username { name -> (name[0] + name[-1]).toUpperCase() }
            hostname { host -> "localhost" }
            ipAddresses { addresses -> addresses.collect { address -> "0.0.0.0"} }
        }
    }
}

rootProject.name = "codewars"

def skip = ["build", "8 kyu", "7 kyu", "6 kyu", "5 kyu", "4 kyu", "3 kyu", "2 kyu", "1 kyu"]
file("kata").traverse(type: groovy.io.FileType.DIRECTORIES, maxDepth: 1) {
    if (!skip.contains(it.name)) {
        include it.name
        project(":${it.name}").projectDir = it
    }
}